= Pipeline Implementation

== Data preparation

A first step in any pipeline is to prepare the input data. You will find
all the data required to run the pipeline in the folder `data`
within the `$HOME/environment/hands-on` repository directory.

There are four data inputs that we will use in this tutorial:

. *Genome File* (`data/genome.fa`)
* Human chromosome 22 in FASTA file format

. *Read Files* (`data/reads/`)
* Sample ENCSR000COQ1: 76bp paired-end reads (`ENCSR000COQ1_1.fq.gz` and `ENCSR000COQ1_2.fq.gz`).

. *Variants File* (`data/known_variants.vcf.gz`)
* Known variants, gzipped as a Variant Calling File (VCF) format.

. *Blacklist File* (`data/blacklist.bed`)
* Genomic locations which are known to produce artifacts and spurious variants in Browser Extensible Data (BED) format.


***


== Input parameters
We can begin writing the pipeline by creating and editing a text file called `main.nf`
from the `$HOME/nf-course/hands-on` repository directory with your favourite text editor. In this example we are using `nano`:

[source,cmd]
----
cd $HOME/nf-course/hands-on
nano main.nf
----

Edit this file to specify the input files as script parameters. Using this notation
allows you to override them by specifying different values when launching the
pipeline execution.

[source,nextflow,linenums]
----
/*
 * Define the default parameters <1>
 */

params.genome     = "$baseDir/data/genome.fa" <2>
params.variants   = "$baseDir/data/known_variants.vcf.gz"
params.blacklist  = "$baseDir/data/blacklist.bed"
params.reads      = "$baseDir/data/reads/ENCSR000COQ1_{1,2}.fastq.gz" <3>
params.results    = "results" <4>
params.gatk       = "/opt/broad/GenomeAnalysisTK.jar" <5>

----

TIP: You can copy the above text by using the kbd:[Cmd+C] keys, then move in the terminal window,
open `nano` and paste the above text by using the kbd:[Cmd+V] keys shortcut.

<1> The `/\*`, `*` and `*/` specify comment lines which are ignored by Nextflow.

<2> The `baseDir` variable represents the main script path location.

<3> The `reads` parameter uses a glob pattern to specify the forward (`ENCSR000COQ1_1.fq.gz`) and reverse (`ENCSR000COQ1_2.fq.gz`) reads are pairs of the same sample.

<4> The `results` parameter is used to specify a directory called `results`.

<5> The `gatk` parameter specifies the location of the GATK jar file.

Once you have the default parameters in the `main.nf` file, you can save and run the main script for the first time.

TIP: With `nano` you can save and close the file with kbd:[Ctrl+O], then kbd:[Enter], followed by kbd:[Ctrl+X].

To run the main script use the following command:

[source,cmd]
----
nextflow run main.nf
----

You should see the script execute, print Nextflow version and pipeline revision and then exit.

----
N E X T F L O W  ~  version 20.01.0
Launching `main.nf` [lethal_faggin] - revision: 4c9a5c830c
----

***
=== Problem #1
Great, now we need to define a https://www.nextflow.io/docs/latest/channel.html[channel] variable
to handle the read-pair files.
To do that open the `main.nf` file and copy the lines below at the end of the file.

TIP: In `nano` you can move to the end of the file using kbd:[Ctrl+W] and then kbd:[Ctrl+V].

This time you must fill the `BLANK` space with the correct function and parameter.

[source,nextflow,linenums]
----
/*
 *  Parse the input parameters
 */

reads_ch        = BLANK
GATK            = params.gatk
----

TIP: Use the https://www.nextflow.io/docs/latest/channel.html#fromfilepairs[fromFilePairs]
channel factory method. The second one, declares a variable named `GATK`
specifying the path of the GATK application file.


Once you think you have data organised, you can again run the pipeline.
However this time, we can use the the `-resume` flag.

[source,cmd]
----
nextflow run main.nf -resume
----


TIP: See https://www.nextflow.io/docs/latest/getstarted.html?highlight=resume#modify-and-resume[here] for more
details about using the `resume` option.

<<solutions/anxious_advice.adoc#, Solution>>

***

== Process 1A
[discrete]
== Create a FASTA genome index

Now we have our inputs set up we can move onto the processes. In our first process we will
create a genome index using http://www.htslib.org/[samtools].

You should implement a process having the following structure:

Name::
1A_prepare_genome_samtools

Command::
create a genome index for the genome fasta with samtools

Input::
the genome fasta file

Output::
the samtools genome index file

=== Problem #2
Copy the code below and paste it at the end of `main.nf`.

Your aim is to replace `BLANK` placeholder with the  the correct
variable name of the genome file that you have defined in previous problem.

[source,nextflow,linenums]
----
/*
 * Process 1A: Create a FASTA genome index with samtools
 */

process '1A_prepare_genome_samtools' { <1>

  input:
    path genome from BLANK <2>

  output:
    path "${genome}.fai" into genome_index_ch <3>

  script:
  """
  samtools faidx ${genome} <4>
  """
}
----

In plain english, the process could be written as:

<1> A **process** called 1A_prepare_genome_samtools

<2> takes as **input** the genome file from `BLANK`

<3> and creates as **output** a genome index file which goes into channel `genome_index_ch`

<4> **script**: using samtools create the genome index from the genome file


Now when we run the pipeline, we see that the process 1A is submitted:

[source,cmd]
----
nextflow run main.nf -resume
----

----
N E X T F L O W  ~  version 20.01.0
Launching `main.nf` [cranky_bose] - revision: d1df5b7267
executor >  local (1)
[cd/47f882] process > 1A_prepare_genome_samtools [100%] 1 of 1 ✔

----

<<solutions/busy_building.adoc#, Solution>>

***
== Process 1B
[discrete]
== Create a FASTA genome sequence dictionary with Picard for GATK

Our first process created the genome index for GATK using samtools. For the next process we must do something very similar, this time creating a genome sequence dictionary using https://broadinstitute.github.io/picard/[Picard].

You should implement a process having the following structure:

Name::
1B_prepare_genome_picard

Command::
create a genome dictionary for the genome fasta with Picard tools

Input::
the genome fasta file

Output::
the genome dictionary file

=== Problem #3

Fill in the `BLANK` words for both the input and output sections.

Copy the code below and paste it at the end of `main.nf`.

Your aim is to insert the correct input name from into
the input step (written as `BLANK`) of the process and run the pipeline.

TIP: You can choose any channel output name that makes sense to you.

[source,nextflow,linenums]
----
/*
 * Process 1B: Create a FASTA genome sequence dictionary with Picard for GATK
 */

process '1B_prepare_genome_picard' {

  input:
    path genome BLANK BLANK

  output:
    path "${genome.baseName}.dict" BLANK BLANK

  script:
  """
  PICARD=`which picard.jar`
  java -jar \$PICARD CreateSequenceDictionary R= $genome O= ${genome.baseName}.dict
  """
}
----

NOTE: `.baseName` returns the filename without the file suffix. If `"${genome}"` is `human.fa`, then `"${genome.baseName}.dict"` would be `human.dict`.

<<solutions/cooing_clock.adoc#, Solution>>

***

== Process 1C
[discrete]
== Create STAR genome index file

Next we must create a genome index for the https://github.com/alexdobin/STAR[STAR] mapping software.

You should implement a process having the following structure:

Name::
1C_prepare_star_genome_index

Command::
create a STAR genome index for the genome fasta

Input::
the genome fasta file

Output::
a directory containing the STAR genome index


=== Problem #4

This is a similar exercise as problem 3, except this time both `input` and `output` lines have been left `BLANK` and must be completed.

[source,nextflow,linenums]
----
/*
 * Process 1C: Create the genome index file for STAR
 */

process '1C_prepare_star_genome_index' {

  input:
      BLANK_LINE

  output:
      BLANK_LINE

  script:
  """
  mkdir genome_dir

  STAR --runMode genomeGenerate \
       --genomeDir genome_dir \
       --genomeFastaFiles ${genome} \
       --runThreadN ${task.cpus}
  """
}
----

TIP: The output of the STAR genomeGenerate command is specified here as `genome_dir`.

<<solutions/discreet_direction.adoc#, Solution>>

***

== Process 1D
[discrete]
== Filtered and recoded set of variants

Next on to something a little more tricky. The next process takes two inputs: the variants
file and the blacklist file.

It should output a channel named `prepared_vcf_ch` which emitting a tuple of two files.

NOTE: In Nextflow, tuples can be defined in the input or output using the https://www.nextflow.io/docs/latest/process.html#input-of-type-tuple[`tuple`] qualifier.

You should implement a process having the following structure:

Name::
1D_prepare_vcf_file

Command::
create a filtered and recoded set of variants

Input::
the variants file +
the blacklisted regions file

Output::
a tuple containing the filtered/recoded VCF file and the tab index (TBI) file.


=== Problem #5

You must fill in the two `BLANK_LINES` in the input and the two `BLANK` output files.

[source,nextflow,linenums]
----
/*
 * Process 1D: Create a file containing the filtered and recoded set of variants
 */

process '1D_prepare_vcf_file' {

  input:
      BLANK_LINE
      BLANK_LINE

  output:
      tuple BLANK, BLANK into prepared_vcf_ch

  script:
  """
  vcftools --gzvcf $variantsFile -c \//<1>
           --exclude-bed ${blacklisted} \//<2>
           --recode | bgzip -c \
           > ${variantsFile.baseName}.filtered.recode.vcf.gz <3>

  tabix ${variantsFile.baseName}.filtered.recode.vcf.gz <4>
  """
}
----
<1> The input variable for the variants file
<2> The input variable for the blacklist file
<3> The first of the two output files
<4> Generates the second output file named `"${variantsFile.baseName}.filtered.recode.vcf.gz.tbi"`

Try run the pipeline from the project directory with:

[source,cmd]
----
nextflow run main.nf -resume
----

<<solutions/expensive_ear.adoc#, Solution>>

Congratulations! Part 1 is now complete.

***

We have all the data prepared and into channels ready for the more serious steps

== Process 2
[discrete]
== STAR Mapping

In this process, for each sample, we align the reads to our genome using the STAR index we created previously.

You should implement a process having the following structure:

Name::
2_rnaseq_mapping_star

Command::
mapping of the RNA-Seq reads using STAR

Input::
the genome fasta file +
the STAR genome index +
a tuple containing the replicate id and paired read files

Output::
a tuple containing replicate id, aligned bam file & aligned bam file index


=== Problem #6

Copy the code below and paste it at the end of `main.nf`.

You must fill in the three `BLANK_LINE` lines in the input and the one `BLANK_LINE` line in the output.

[source,nextflow,linenums]
----
/*
 * Process 2: Align RNA-Seq reads to the genome with STAR
 */

process '2_rnaseq_mapping_star' {

  input:
      BLANK_LINE
      BLANK_LINE
      BLANK_LINE

  output:
      BLANK_LINE

  script:
  """
  # ngs-nf-dev Align reads to genome
  STAR --genomeDir $genomeDir \
       --readFilesIn $reads \
       --runThreadN ${task.cpus} \
       --readFilesCommand zcat \
       --outFilterType BySJout \
       --alignSJoverhangMin 8 \
       --alignSJDBoverhangMin 1 \
       --outFilterMismatchNmax 999

  # 2nd pass (improve alignmets using table of splice junctions and create a new index)
  mkdir genomeDir
  STAR --runMode genomeGenerate \
       --genomeDir genomeDir \
       --genomeFastaFiles $genome \
       --sjdbFileChrStartEnd SJ.out.tab \
       --sjdbOverhang 75 \
       --runThreadN ${task.cpus}

  # Final read alignments
  STAR --genomeDir genomeDir \
       --readFilesIn $reads \
       --runThreadN ${task.cpus} \
       --readFilesCommand zcat \
       --outFilterType BySJout \
       --alignSJoverhangMin 8 \
       --alignSJDBoverhangMin 1 \
       --outFilterMismatchNmax 999 \
       --outSAMtype BAM SortedByCoordinate \
       --outSAMattrRGline ID:$replicateId LB:library PL:illumina PU:machine SM:GM12878

  # Index the BAM file
  samtools index Aligned.sortedByCoord.out.bam
  """
}
----

TIP: The final command produces an bam index which is the full filename with an additional `.bai` suffix.

<<solutions/fat_floor.adoc#, Solution>>

***

The next step is a filtering step using GATK. For each sample, we split all the reads that contain
N characters in their http://genome.sph.umich.edu/wiki/SAM#What_is_a_CIGAR.3F[CIGAR] string.

== Process 3
[discrete]
== GATK Split on N

The process creates k+1 new reads (where k is the number of N cigar elements)
that correspond to the segments of the original read beside/between
the splicing events represented by the Ns in the original CIGAR.

You should implement a process having the following structure:

Name::
3_rnaseq_gatk_splitNcigar

Command::
split reads on Ns in CIGAR string using GATK

Input::
the genome fasta file +
the genome index made with samtools +
the genome dictionary made with picard +
a tuple containing replicate id, aligned bam file and aligned bam file index from the STAR mapping

Output::
a tuple containing the replicate id, the split bam file and the split bam index file


=== Problem #7

Copy the code below and paste it at the end of `main.nf`.

You must fill in the four `BLANK_LINE` lines in the input and the one `BLANK_LINE` line in the output.

CAUTION: There is an optional https://www.nextflow.io/docs/latest/process.html#tag[`tag`] line added
to the start of this process. The https://www.nextflow.io/docs/latest/process.html#tag[`tag`] line
allows you to assign a name to a specific task (single execution of a process).
This is particularly useful when there are many samples/replicates which pass through the same process.

[source,nextflow,linenums]
----
process '3_rnaseq_gatk_splitNcigar' {
  tag OPTIONAL_BLANK

  input:
      BLANK_LINE
      BLANK_LINE
      BLANK_LINE
      BLANK_LINE

  output:
      BLANK_LINE

  script:
  """
  # SplitNCigarReads and reassign mapping qualities
  java -jar $GATK -T SplitNCigarReads \
                  -R $genome -I $bam \
                  -o split.bam \
                  -rf ReassignOneMappingQuality \
                  -RMQF 255 -RMQT 60 \
                  -U ALLOW_N_CIGAR_READS \
                  --fix_misencoded_quality_scores
  """
}
----

TIP: The GATK command above automatically creates a bam index (`.bai`) of the `split.bam` output file

TIP: A `tag` line would also be useful in <<Process 2>>

<<solutions/gentle_garden.adoc#, Solution>>

***

Next we perform a Base Quality Score Recalibration step using GATK.

== Process 4
[discrete]
== GATK Recalibrate

This step uses GATK to detect systematic errors in the base quality scores, select unique alignments and then index the resulting bam file with samtools. You can find details of the specific GATK BaseRecalibrator parameters https://software.broadinstitute.org/gatk/documentation/tooldocs/3.8-0/org_broadinstitute_gatk_tools_walkers_bqsr_BaseRecalibrator.php[here].

You should implement a process having the following structure:

Name::
4_rnaseq_gatk_recalibrate

Command::
recalibrate reads from each replicate using GATK

Input::
the genome fasta file +
the genome index made with samtools +
the genome dictionary made with picard +
a tuple containing replicate id, aligned bam file and aligned bam file index from process 3 +
a tuple containing the filtered/recoded VCF file and the tab index (TBI) file from process 1D +

Output::
a tuple containing the sample id, the unique bam file and the unique bam index file


=== Problem #8

Copy the code below and paste it at the end of `main.nf`.

You must fill in the five `BLANK_LINE` lines in the input and the one `BLANK` in the output line.

[source,nextflow,linenums]
----
process '4_rnaseq_gatk_recalibrate' {
  tag "$replicateId"

  input:
      BLANK_LINE
      BLANK_LINE
      BLANK_LINE
      BLANK_LINE
      BLANK_LINE

  output:
      BLANK into (final_output_ch, bam_for_ASE_ch) <1>

  script:
    sampleId = replicateId.replaceAll(/[12]$/,'')
    """
    # Indel Realignment and Base Recalibration
    java -jar $GATK -T BaseRecalibrator \
                  --default_platform illumina \
                  -cov ReadGroupCovariate \
                  -cov QualityScoreCovariate \
                  -cov CycleCovariate \
                  -knownSites ${prepared_variants_file} \
                  -cov ContextCovariate \
                  -R ${genome} -I ${bam} \
                  --downsampling_type NONE \
                  -nct ${task.cpus} \
                  -o final.rnaseq.grp

     java -jar $GATK -T PrintReads \
                  -R ${genome} -I ${bam} \
                  -BQSR final.rnaseq.grp \
                  -nct ${task.cpus} \
                  -o final.bam

    # Select only unique alignments, no multimaps
    (samtools view -H final.bam; samtools view final.bam| grep -w 'NH:i:1') \
    |samtools view -Sb -  > ${replicateId}.final.uniq.bam <2>

    # Index BAM files
    samtools index ${replicateId}.final.uniq.bam <3>
    """
}

----
<1> The files resulting from this process will be used in two downstream processes. If a process is executed more than once, and the downstream channel is used by more than one process, we must duplicate the channel. We can do this using the `into` operator with parenthesis in the output section. See https://www.nextflow.io/docs/latest/operator.html#into[here] for more information on using `into`.
<2> The unique bam file
<3> The index of the unique bam file (bam file name + `.bai`)

<<solutions/hulking_hospital.adoc#, Solution>>

***

Now we are ready to perform the variant calling with GATK.



== Process 5
[discrete]
== GATK Variant Calling

This steps call variants with GATK HaplotypeCaller. You can find details of the specific
GATK HaplotypeCaller parameters https://software.broadinstitute.org/gatk/documentation/tooldocs/current/org_broadinstitute_gatk_tools_walkers_haplotypecaller_HaplotypeCaller.php[here].

You should implement a process having the following structure:

Name::
5_rnaseq_call_variants

Command::
variant calling of each sample using GATK

Input::
the genome fasta file +
the genome index made with samtools +
the genome dictionary made with picard +
a tuple containing replicate id, aligned bam file and aligned bam file index from process 4

Output::
a tuple containing the sample id the resulting variant calling file (vcf)


=== Problem #9

In this problem we will introduce the use of a channel operator in the input section.
The https://www.nextflow.io/docs/latest/operator.html#grouptuple[groupTuple] operator
groups together the tuples emitted by a channel which share a common key.

CAUTION: Note that in process 4, we used the sampleID (not replicateID) as the first element
of the tuple in the output. Now we combine the replicates by grouping them on the sample ID.
It follows from this that process 4 is run one time per replicate and process 5 is run one time per sample.

Fill in the `BLANK_LINE` lines and `BLANK` words as before.

[source,nextflow,linenums]
----
process '5_rnaseq_call_variants' {
  tag BLANK

  input:
      BLANK_LINE
      BLANK_LINE
      BLANK_LINE
      BLANK from BLANK.groupTuple()

  output:
      BLANK_LINE

  script:
  """
  echo "${bam.join('\n')}" > bam.list

  # Variant calling
  java -jar $GATK -T HaplotypeCaller \
                  -R $genome -I bam.list \
                  -dontUseSoftClippedBases \
                  -stand_call_conf 20.0 \
                  -o output.gatk.vcf.gz

  # Variant filtering
  java -jar $GATK -T VariantFiltration \
                  -R $genome -V output.gatk.vcf.gz \
                  -window 35 -cluster 3 \
                  -filterName FS -filter "FS > 30.0" \
                  -filterName QD -filter "QD < 2.0" \
                  -o final.vcf
  """
}
----

<<solutions/imported_iron.adoc#, Solution>>

***


== Processes 6A and 6B
[discrete]
== ASE & RNA Editing

In the final steps we will create processes for Allele-Specific Expression and RNA Editing Analysis.

We must process the VCF result to prepare variants file for allele specific expression (ASE) analysis. We will implement both processes together.

You should implement two processes having the following structure:

.1st process
Name::
6A_post_process_vcf

Command::
post-process the variant calling file (vcf) of each sample

Input::
tuple containing the sample ID and vcf file +
a tuple containing the filtered/recoded VCF file and the tab index (TBI) file from process 1D +

Output::
a tuple containing the sample id, the variant calling file (vcf) and a file containing common SNPs

.2nd process
Name::
6B_prepare_vcf_for_ase

Command::
prepare the VCF for allele specific expression (ASE) and generate a figure in R.

Input::
a tuple containing the sample id, the variant calling file (vcf) and a file containing common SNPs

Output::
a tuple containing the sample ID and known SNPs in the sample for ASE +
a figure of the SNPs generated in R as a PDF file


=== Problem #10

Here we introduce the `publishDir` directive. This allows us to specifiy a location for the outputs of the process. See https://www.nextflow.io/docs/latest/process.html#publishdir[here] for more details.

You must have the output of process 6A become the input of process 6B.

[source,nextflow,linenums]
----
process '6A_post_process_vcf' {
  tag BLANK
  publishDir "$params.results/$sampleId" <1>

  input:
      BLANK_LINE
      BLANK_LINE

  output:
      BLANK_LINE

  script:
  '''
  grep -v '#' final.vcf | awk '$7~/PASS/' |perl -ne 'chomp($_); ($dp)=$_=~/DP\\=(\\d+)\\;/; if($dp>=8){print $_."\\n"};' > result.DP8.vcf

  vcftools --vcf result.DP8.vcf --gzdiff filtered.recode.vcf.gz  --diff-site --out commonSNPs
  '''
}


process '6B_prepare_vcf_for_ase' {
  tag BLANK
  publishDir BLANK

  input:
      BLANK_LINE
  output:
      BLANK_LINE
      BLANK_LINE

  script:
  '''
  awk 'BEGIN{OFS="\t"} $4~/B/{print $1,$2,$3}' commonSNPs.diff.sites_in_files  > test.bed

  vcftools --vcf final.vcf --bed test.bed --recode --keep-INFO-all --stdout > known_snps.vcf

  grep -v '#'  known_snps.vcf | awk -F '\\t' '{print $10}' \
               |awk -F ':' '{print $2}'|perl -ne 'chomp($_); \
               @v=split(/\\,/,$_); if($v[0]!=0 ||$v[1] !=0)\
               {print  $v[1]/($v[1]+$v[0])."\\n"; }' |awk '$1!=1' \
               >AF.4R

  gghist.R -i AF.4R -o AF.histogram.pdf
  '''
}
----
<1> here the output location is specified as a combination of a pipeline parameter and a process input variable

<<solutions/jumping_jack.adoc#, Solution>>

***
The final step is the GATK ASEReadCounter.


=== Problem #11

We have seen the basics of using processes in Nextflow. Yet one of the 
features of Nextflow is the operations that can be performed on
channels outside of processes. See https://www.nextflow.io/docs/latest/operator.html[here]
for details on the specific operators.

Before we perform the GATK ASEReadCounter process, we must group the data for allele-specific expression. To do this we must combine channels.

The `bam_for_ASE_ch` channel emites tuples having the following structure, holding the final BAM/BAI files:

[source,bash]
----
< sample_id, file_bam, file_bai >
----

The `vcf_for_ASE` channel emits tuples having the following structure:

[source,bash]
----
< sample_id, output.vcf >
----

In the first operation, the BAMs are grouped together by sample id.

Next, this resulting channel is merged with the VCFs (vcf_for_ASE) having the same sample id.

We must take the merged channel and creates a channel named `grouped_vcf_bam_bai_ch` emitting the following tuples:

[source,bash]
----
< sample_id, file_vcf, List[file_bam], List[file_bai] >
----

Your aim is to fill in the `BLANKS` below.

[source,nextflow,linenums]
----
bam_for_ASE_ch
  .BLANK                            <1>
  .phase(vcf_for_ASE)               <2>
  .map{ left, right ->              <3>
    def sampleId = left[0]          <4>
    def bam = left[1]               <5>
    def bai = left[2]               <6>
    def vcf = right [1]             <7>
    tuple(BLANK, vcf, BLANK, BLANK) <8>
  }
  .set { grouped_vcf_bam_bai_ch }   <9>

----
<1> an operator that groups tuples that contain a common first element.
<2> the phase operator synchronizes the values emitted by two other channels. See https://www.nextflow.io/docs/latest/operator.html?phase#phase[here] for more details
<3> the map operator can apply any function to every item on a channel. In this case we take our tuple from the phase operation, define the seperate elements and create a new tuple.
<4> define `sampleId` to be the first element of left.
<5> define bam to be the second element of left.
<6> define bai to be the third element of left.
<7> define vcf to be the first element of right.
<8> create a new tuple made of four elements
<9> rename the resulting as `grouped_vcf_bam_bai_ch`

CAUTION: `left` and `right` above are arbitary names. From the phase operator documentation, we see that phase returns pairs of items. So here `left` originates from contents of the `bam_for_ASE_ch` channel and `right` originates from the contents of `vcf_for_ASE` channel.

<<solutions/kind_koala.adoc#, Solution>>

***

== Process 6C
[discrete]
== Allele-Specific Expression analysis with GATK ASEReadCounter

Now we are ready for the final process.

You should implement a process having the following structure:

Name::
6C_ASE_knownSNPs

Command::
calculate allele counts at a set of positions with GATK tools

Input::
genome fasta file +
genome index file from samtools +
genome dictionary file +
the `grouped_vcf_bam_bai_ch`channel

Output::
the allele specific expression file (`ASE.tsv`)

=== Problem #12

You should construct the process and run the pipeline in its entirety.

[source,nextflow,linenums]
----
  echo "${bam.join('\n')}" > bam.list

  java -jar $GATK -R ${genome} \
                  -T ASEReadCounter \
                  -o ASE.tsv \
                  -I bam.list \
                  -sites ${vcf}
----

<<solutions/laughing_lynx.adoc#, Solution>>

Congratulations! If you made it this far you now have all the basics to create your own Nextflow workflows.

***

include::pipeline_results.adoc[leveloffset=+1]

== Bonus step

Until now the pipeline has been executed using just a single sample (`ENCSR000COQ1`).

Now we can re-execute the pipeline specifying a large set of samples by using the command
shown below:

[source,cmd]
----
nextflow run main.nf -resume --reads 'data/reads/ENCSR000C*_{1,2}.fastq.gz'
----

It will print an output similar to the one below:

----
N E X T F L O W  ~  version 20.01.0
Launching `main.nf` [hungry_wing] - revision: a6359031a1
executor >  local (27)
[cd/47f882] process > 1A_prepare_genome_samtools               [100%] 1 of 1, cached: 1 ✔
[5f/216ba8] process > 1B_prepare_genome_picard                 [100%] 1 of 1, cached: 1 ✔
[76/5fdc20] process > 1C_prepare_star_genome_index             [100%] 1 of 1, cached: 1 ✔
[19/f8842c] process > 1D_prepare_vcf_file                      [100%] 1 of 1, cached: 1 ✔
[f1/d66ba8] process > 2_rnaseq_mapping_star (6)                [100%] 6 of 6, cached: 1 ✔
[74/c0f3a3] process > 3_rnaseq_gatk_splitNcigar (ENCSR000CPO2) [100%] 6 of 6, cached: 1 ✔
[b6/59d9f7] process > 4_rnaseq_gatk_recalibrate (ENCSR000CPO2) [100%] 6 of 6, cached: 1 ✔
[22/4a07fa] process > 5_rnaseq_call_variants (ENCSR000CPO)     [100%] 3 of 3 ✔
[1a/c68bfe] process > 6A_post_process_vcf (ENCSR000CPO)        [100%] 3 of 3 ✔
[dc/e58d02] process > 6B_prepare_vcf_for_ase (ENCSR000CPO)     [100%] 3 of 3 ✔
[2a/0e4e7b] process > 6C_ASE_knownSNPs (ENCSR000CPO)           [100%] 3 of 3 ✔
----

You can notice that this time the pipeline spawns the execution of more tasks because
three samples have been provided instead of one.

This shows the ability of Nextflow to implicitly handle multiple parallel task executions
depending on the specified pipeline input dataset.

A fully functional version of this pipeline is available at the following GitHub repository:
https://github.com/CRG-CNAG/CalliNGS-NF[CalliNGS-NF]. 

