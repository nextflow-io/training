= Nextflow configuration 

A key Nextflow feature is the ability to decouple the workflow implementation
by the configuration setting required by the underlying execution platform. 

This enables portable deployment without the need to modify the application code. 

== Configuration file 

When a pipeline script is launched, Nextflow looks for a file named `nextflow.config` in the current directory and in the script base directory (if it is not the same as the current directory). Finally it checks for the file: `$HOME/.nextflow/config`.

When more than one of the above files exists, they are merged, so that the settings in the first override the same settings that may appear in the second, and so on.

The default config file search mechanism can be extended by providing an extra configuration file by using the command line option: `-c <config file>`.

=== Config syntax 

A Nextflow configuration file is a simple text file containing a set of properties defined using the syntax:

```
name = value
```

TIP: Please note, string values need to be wrapped in quotation characters while numbers and boolean values (`true`, `false`) do not. Also, note that values are typed, meaning for example that, `1` is different from `'1'`, since the first is interpreted as the number one, while the latter is interpreted as a string value.

=== Config variables 

Configuration properties can be used as variables in the configuration file itself, by using the usual `$propertyName` or `${expression}` syntax.

[source,config,linenums]
----
propertyOne = 'world'
anotherProp = "Hello $propertyOne"
customPath = "$PATH:/my/app/folder"
----

TIP: In the configuration file it's possible to access any variable defined in the host environment 
such as `$PATH`, `$HOME`, `$PWD`, etc. 

=== Config comments 

Configuration files use the same conventions for comments used in the Nextflow script:

[source,nextflow,linenums]
----
// comment a single line

/* 
   a comment spanning 
   multiple lines 
 */
----

=== Config scopes 

Configuration settings can be organized in different scopes by dot prefixing the property names with a scope
identifier or grouping the properties in the same scope using the curly brackets notation. This is shown in the following example:

[source,config,linenums]
----
alpha.x  = 1
alpha.y  = 'string value..'

beta {
    p = 2
    q = 'another string ..'
}
----

=== Config params 

The scope `params` allows the definition of workflow parameters that override the values defined 
in the main workflow script. 

This is useful to consolidate one or more execution parameters in a separate file. 

[source,config,linenums]
----
// config file 
params.foo = 'Bonjour'
params.bar = 'le monde!' 
----

[source,nextflow,linenums]
----
// workflow script 
params.foo = 'Hello'
params.bar = 'world!'

// print both params
println "$params.foo $params.bar"
----

[discrete]
=== Exercise

Save the first snippet as `nextflow.config` and the second one as `params.nf`. Then run: 

```cmd
nextflow run params.nf
```

.For the result, click here:
[%collapsible]
====
[source,nextflow]
----
Bonjour le monde!
----
====

Execute is again specifying the `foo` parameter on the command line: 

```cmd
nextflow run params.nf --foo Hola
```

.For the result, click here:
[%collapsible]
====
[source,nextflow]
----
Hola le monde!
----
====

Compare the result of the two executions. 

=== Config env 

The `env` scope allows the definition of one or more variables that will be exported into the environment where the workflow tasks will be executed.

[source,config,linenums]
----
env.ALPHA = 'some value'
env.BETA = "$HOME/some/path"
----

[discrete]
=== Exercise

Save the above snippet a file named `my-env.config`. The save the snippet below in a file named 
`foo.nf`:

[source,nextflow,linenums]
----
process foo {
  echo true
  '''
  env | egrep 'ALPHA|BETA'
  '''
}
----

Finally executed the following command: 

```
nextflow run foo.nf -c my-env.config
```

.Your result should look something like the following, click here:
[%collapsible]
====
[source,nextflow]
----
BETA=/home/some/path
ALPHA=some value
----
====

=== Config process

`process` https://www.nextflow.io/docs/latest/process.html#directives[directives] allow the specification of settings for the task execution such as `cpus`, `memory`, `container` and other resources in the pipeline script. 

This is useful when prototyping a small workflow script. 

However it's always a good practice to decouple the workflow execution 
logic from the process configuration settings, i.e. it's strongly 
suggested to define the process settings in the workflow configuration 
file instead of the workflow script. 

The `process` configuration scope allows the setting of any `process` https://www.nextflow.io/docs/latest/process.html#directives[directives] in the Nextflow configuration file. For example:

[source,config,linenums]
----
process {
    cpus = 10
    memory = 8.GB
    container = 'biocontainers/bamtools:v2.4.0_cv3'
}
----

The above config snippet defines the `cpus`, `memory` and `container`
directives for all processes in your workflow script. 

The https://www.nextflow.io/docs/latest/config.html#process-selectors[process selector] can be used to apply the configuration to a specific
process or group of processes (discussed later). 

TIP: Memory and time duration units can be specified either 
using a string based notation in which the digit(s) and the unit 
*can* be separated by a blank or by using the numeric notation in 
which the digit(s) and the unit are separated by a dot character and 
are not enclosed by quote characters. 

[%header,cols=3*] 
|===
|String syntax
|Numeric syntax
|Value

|`'10 KB'`
|`10.KB`
| 10240 bytes

|`'500 MB'`
|`500.MB`
| 524288000 bytes

|`'1 min'`
|1.min
|60 seconds

|`'1 hour 25 sec'`
|-
|1 hour and 25 seconds 
|===


The syntax for setting `process` directives in the 
configuration file requires `=` (i.e. assignment operator), whereas
it should not be used when setting the process directives within the 
workflow script. 


.For an example, click here:
[%collapsible]
====
process foo {
  cpus 4
  memory 2.GB 
  time 1.hour
  maxRetries 3   

  script:
  """
    your_command --cpus $task.cpus --mem $task.memory
  """
}
====

This is especially important when you want to define a config setting
using a dynamic expression using a closure. For example: 

```
process foo {
    memory = { 4.GB * task.cpus }
}
```

Directives that requires more than one value, e.g. https://www.nextflow.io/docs/latest/process.html#pod[pod], in the 
configuration file need to be expressed as a map object. 

```
process {
    pod = [env: 'FOO', value: '123']
}
```

Finally directives that allows to be repeated in the process 
definition, in the configuration files need to be defined 
as a list object. For example:

```
process {
    pod = [ [env: 'FOO', value: '123'], 
            [env: 'BAR', value: '456'] ]
}
```

=== Config Docker execution

The container image to be used for the process execution can be specified in the `nextflow.config` 
file:

[source,config,linenums]
----
process.container = 'nextflow/rnaseq-nf'
docker.enabled = true
----

The use of unique "SHA256" docker image IDs guarantees that the image content does not change
over time, for example:

[source,config,linenums,options="nowrap"]
----
process.container = 'nextflow/rnaseq-nf@sha256:aeacbd7ea1154f263cda972a96920fb228b2033544c2641476350b9317dab266'
docker.enabled = true
----

=== Config Singularity execution 

To run a workflow execution with Singularity, a container 
image file path is required in the Nextflow config file using the container directive: 

[source,config,linenums,options="nowrap"]
----
process.container = '/some/singularity/image.sif'
singularity.enabled = true
----

WARNING: The container image file must be an absolute path i.e. it must start with a `/`. 

The following protocols are supported: 

* `library://` download the container image from the https://cloud.sylabs.io/library[Singularity Library service].
* `shub://` download the container image from the https://singularity-hub.org/[Singularity Hub].
* `docker://` download the container image from the https://hub.docker.com/[Docker Hub] and convert it to 
the Singularity format.
* `docker-daemon://` pull the container image from a local Docker installation and convert it to a Singularity image file.

WARNING: * Singularity hub `shub://` is no longer available as a builder service. Though existing images from before 19th April 2021 will still work.

TIP: By specifying a plain Docker container image name, Nextflow implicitly downloads and converts it to 
a Singularity image when the Singularity execution is enabled. For example:

[source,config,linenums,options="nowrap"]
----
process.container = 'nextflow/rnaseq-nf'
singularity.enabled = true
----

The above configuration instructs Nextflow to use the Singularity engine to run
your script processes. The container is pulled from the Docker registry and cached
in the current directory to be used for further runs.

Alternatively if you have a Singularity image file, its absolute path location 
can be specified as the container name either using the `-with-singularity` option
or the `process.container` setting in the config file.

[discrete]
=== Exercise 

Try to run the script as shown below, changing the `nextflow.config` file to the one above using `singularity`:

```bash
nextflow run script7.nf
```

TIP: Nextflow will pull the container image automatically, it will require a few seconds
depending the network connection speed.

=== Config Conda execution

The use of a Conda environment can also be provided in the configuration file
adding the following setting in the `nextflow.config` file:

[source,config,linenums,options="nowrap"]
----
process.conda = "/home/ubuntu/miniconda2/envs/nf-tutorial"
----

You can specify the path of an existing Conda environment as either *directory* or the 
path of Conda environment YAML file. 

